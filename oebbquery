#!/usr/bin/perl
use strict;
use warnings;
use LWP::UserAgent;

# name:     oebbquery
# author:   ap0calypse (ap0calypse@agitatio.org)
# version:  2010-06-17
#           2011-01-20 (update because of website changes of the oebb)
# purpose:  does a web-query on the website of the OEBB (www.oebb.at) to 
#           look up a connection from A to B. 
# command:  ./oebbquery [FROM] [TO]

my $ua = LWP::UserAgent->new;

# get the locations from the commandline
my ($from, $to) = @ARGV;

# we use current time and date
my $whendate = sprintf("%s.%s.%s", (localtime)[3], (localtime)[4] + 1, (localtime)[5] + 1900);
my $whentime = sprintf("%s:%s", (localtime)[2], (localtime)[1]);

# comment this out and change it if you need a proxy-server
# $ua->proxy('http', 'http://user@proxy:port');

# here is the main query
my $response = $ua->get("http://fahrplan.oebb.at/bin/query.exe/dn?start=1&_charset_=UTF-8&S=$from" .
                        "&Z=$to&timesel=depart&time=$whentime&REQ0" .
                        "JourneyDate=$whendate");



# removed the obsolete html-tempfile and replaced it with an array here,
# thanks to stefan for pointing this out :)
my @html_data;
if ($response->is_success) {
    @html_data = split /\n/, $response->decoded_content;
}
else {
    die $response->status_line;
}

print "Connections from $from to $to:\n\n"; 
print "Departure  Arrival  Duration  Changes  Train(s)\n";

# my array of hashes for the train information and the needed variables
my @AoH = ({});
my ($deptime, $arrtime, $dur, $train, $changes);
my $run = 0;
my $i = 1;
my $uninteresting = 500;        # first 500 lines are uninteresting


for (@html_data) {
    $i++;
    next if $i < $uninteresting;
    last if m/.*Hinweise zur Prognose.*/gms;
    if(m/^(\d{2}:\d{2}) ab<br \/>.*/gms) {
        $deptime = $1;
        $AoH[$run] = { 'id' => $run, 'dep' => $deptime };
    }
    if(m/^(\d{2}:\d{2}) an.*/gms) {
        $arrtime = $1;
        ${$AoH[$run]}{'arr'} = $arrtime;
    }
    if (m/^(\d{1,2}:\d{2}).*$/gms) {
        $dur = sprintf("%5s", $1);
        ${$AoH[$run]}{'dur'} = $dur;
    }
    if (m/^<td headers="hafasOVChanges".+>(\d)+<\/td.*$/gms) {
        $changes = $1;
        ${$AoH[$run]}{'chg'} = $changes;
    }
    if (m/^(?:<img class="product" src="\/img\/vs_oebb\/(\w+)_pic.gif".*\/>)+/gms) {
        my @trains = split /></, $_;
        for (@trains) {
            if (m/(?:img class="product" src="\/img\/vs_oebb\/(\w+)_pic.gif".*\/)+/gms) {
                $train .= "$1 ";
            }
        }
        ${$AoH[$run]}{'train'} = $train;
        $train = "";
        $run++;
    }
}

# main output
for my $hashref (@AoH) {
    print $$hashref{'dep'}, " " x 6, $$hashref{'arr'}, " " x 3, 
          $$hashref{'dur'}, " " x 6, $$hashref{'chg'}, " " x 8, 
          $$hashref{'train'}, "\n";
}
